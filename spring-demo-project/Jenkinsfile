pipeline {
    agent {
        label 'docker0'
    }

    environment {
        C_VERSION = "v0.${env.BUILD_NUMBER}.0"
    }

    stages {
        stage('Checkout') {
            steps {
                checkout scm
            }
        }

        stage('Build') {
            steps {
                sh "./gradlew -Pversion=${env.C_VERSION} build"
            }
        }

        stage('QA') {
            when {
                expression {
                    withSonarQubeEnv('sonar') {
                        sh "./gradlew -Pversion=${env.C_VERSION} --info sonarqube"
                    }
                    sleep 10
                    return waitForQualityGate().status != 'OK'
                }
            }
            steps {
                error "Pipeline aborted due to quality gate failure"
            }
        }

        stage('Publish JAR') {
            steps {
                nexusArtifactUploader artifacts: [
                    [artifactId: 'demo',
                    classifier: 'jar',
                    file: "build/libs/demo-${env.C_VERSION}.jar",
                    type: 'jar']
                ], credentialsId: 'bitbucket', groupId: 'com.example', nexusUrl: 'nexus:8081', nexusVersion: 'nexus3', protocol: 'http', repository: 'maven-releases', version: "${env.C_VERSION}"
            }
        }

        stage('Publish Docker') {
            steps {
                sh "curl -o app.jar http://nexus:8081/repository/maven-releases/com/example/demo/${env.C_VERSION}/demo-${env.C_VERSION}-jar.jar"
                script {
                    docker.withServer('tcp://socatdockersock:2375') {
                        docker.withRegistry('http://nexus:20000', 'bitbucket') {
                            docker.build("demo").push("${env.C_VERSION}")
                        }
                    }
                }
            }
        }

        stage('Deploy') {
            steps {
                script {
                    docker.withServer('tcp://socatdockersock:2375') {
                        sh """docker run --net jenkins0_default \
                        --name demo${env.C_VERSION} -d -p 10080 nexus:20000/demo:${env.C_VERSION}"""
                    }
                }
            }
        }

        stage('Post-deploy check') {
            steps {
                timeout(time: 3, unit: 'MINUTES') {
                    retry(100) {
                        sleep 1
                        sh "curl http://demo${env.C_VERSION}:10080/health"
                    }
                }
            }
        }
    }

    post {
        always {
            script {
                docker.withServer('tcp://socatdockersock:2375') {
                    sh "docker rm -f demo${env.C_VERSION}"
                    sh "docker rmi -f nexus:20000/demo:${env.C_VERSION}"
                }
            }
        }
    }
}
